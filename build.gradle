plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "io.github.goooler.shadow" version "8.1.8"
}

group = 'org.nmo.project_exfil'
version = 'v1.14.514'

repositories {
    mavenCentral()
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io/"
    }
    maven {
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "citizens-repo"
        url = "https://maven.citizensnpcs.co/repo"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "is-snapshots"
        url = "https://repo.infernalsuite.com/repository/maven-snapshots/"
    }
    maven {
        name = "multiverse"
        url = "https://repo.onarandombox.com/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven { url 'https://repo.codemc.io/repository/creatorfromhell/' }
    maven {
        name = "alessiodp-repo"
        url = "https://repo.alessiodp.com/releases/"
    }

    // ItemsAdder API
    maven {
        name = "matteodev"
        url = "https://maven.devs.beer/"
    }

}

dependencies {
    compileOnly("com.github.Gecolay.GSit:core:3.0.1")
    compileOnly('net.citizensnpcs:citizens-main:2.0.35-SNAPSHOT') {
        exclude group: '*', module: '*'
    }
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    
    // Inventory Framework
    implementation "com.github.stefvanschie.inventoryframework:IF:0.11.5"
    
    // WorldEdit / FAWE
    compileOnly "com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.14.2"
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.14.2") {
        transitive = false
    }

    // Advanced Slime Paper API
    implementation "com.infernalsuite.asp:api:4.0.0-SNAPSHOT"
    implementation "com.infernalsuite.asp:file-loader:4.0.0-SNAPSHOT"
    
    // PlaceholderAPI
    compileOnly 'me.clip:placeholderapi:2.11.7'
    
    // DecentHolograms
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.9.7'

    // TAB
    compileOnly 'com.github.NEZNAMY:TAB-API:5.3.2'

    // VaultUnlockedAPI
    compileOnly "net.milkbowl.vault:VaultUnlockedAPI:2.16"

    // LuckPerms
    compileOnly 'net.luckperms:api:5.4'

    // XConomyAPI
    compileOnly "com.github.YiC200333:XConomyAPI:2.25.1"

    // Parties
    compileOnly group: 'com.alessiodp.parties', name: 'parties-api', version: '3.2.16'

    // Adventure
    compileOnly "net.kyori:adventure-api:4.19.0"
    compileOnly "net.kyori:adventure-text-minimessage:4.19.0"

    // ProtocolLib
    compileOnly 'net.dmulloy2:ProtocolLib:5.4.0'

    // QualityArmory
    compileOnly 'me.zombie_striker:QualityArmory:2.1.0'

    // Citizens
    compileOnly('net.citizensnpcs:citizens-main:2.0.35-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

    // Sentinel
    compileOnly 'org.mcmonkey:sentinel:2.9.2-SNAPSHOT'

    // ItemsAdder API (provided by ItemsAdder plugin at runtime)
    // Use dynamic version to avoid hardcoding; pin this if you need reproducible builds.
    compileOnly 'dev.lone:api-itemsadder:+' 

}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.8")
        serverJar(file("asp-server.jar"))
        
        // Download and install plugins for the test server
        downloadPlugins {
            modrinth("fastasyncworldedit", "2.14.2")

            // PlaceholderAPI
            url("https://github.com/PlaceholderAPI/PlaceholderAPI/releases/download/2.11.7/PlaceholderAPI-2.11.7.jar")

            // DecentHolograms
            modrinth("decentholograms", "2.9.8")

            // TAB
            modrinth("tab-was-taken", "5.3.2")

            // LuckPerms
            modrinth("luckperms", "v5.5.17-bukkit")

            // VaultUnlocked
            modrinth("vaultunlocked", "2.17.0")

            // ProtocolLib
            url("https://github.com/dmulloy2/ProtocolLib/releases/download/5.4.0/ProtocolLib.jar")

            // QualityArmory
            modrinth("qualityarmory", "2.1.0")

            // XConomy (Economy Provider)
            url("https://github.com/YiC200333/XConomy/releases/download/2.26.3/XConomy-Paper-2.26.3.jar")
        }
    }
    
    shadowJar {
        relocate 'com.github.stefvanschie.inventoryframework', 'org.nmo.project_exfil.inventoryframework'
    }
    
    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
