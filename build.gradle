plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "io.github.goooler.shadow" version "8.1.8"
}

group = 'org.nmo.project_exfil'
version = 'v1.14.514'

repositories {
    mavenCentral()
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "citizens-repo"
        url = "https://maven.citizensnpcs.co/repo"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
    // maven {
    //     name = "intellectualsites"
    //     url = "https://repo.intellectualsites.com/repository/public/"
    // }
    maven {
        name = "multiverse"
        url = "https://repo.onarandombox.com/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven { url 'https://repo.codemc.io/repository/creatorfromhell/' }
}

dependencies {
    compileOnly("com.github.Gecolay.GSit:core:3.0.1")
    compileOnly('net.citizensnpcs:citizens-main:2.0.35-SNAPSHOT') {
        exclude group: '*', module: '*'
    }
    compileOnly("io.papermc.paper:paper-api:1.21.6-R0.1-SNAPSHOT")
    
    // Inventory Framework
    implementation "com.github.stefvanschie.inventoryframework:IF:0.11.5"
    
    // WorldEdit / FAWE
    compileOnly "com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.11.1"
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.11.1") {
        transitive = false
    }
    
    // WorldGuard
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.10"
    
    // Multiverse-Core
    compileOnly "org.mvplugins.multiverse.core:multiverse-core:5.0.0-SNAPSHOT"
    
    // PlaceholderAPI
    compileOnly 'me.clip:placeholderapi:2.11.7'
    
    // DecentHolograms
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.9.7'

    // TAB
    compileOnly 'com.github.NEZNAMY:TAB-API:5.3.2'

    // VaultUnlockedAPI
    compileOnly "net.milkbowl.vault:VaultUnlockedAPI:2.16"

    // LuckPerms
    compileOnly 'net.luckperms:api:5.4'

    // XConomyAPI
    compileOnly "com.github.YiC200333:XConomyAPI:2.25.1"
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.6")
        
        // Download and install plugins for the test server
        downloadPlugins {
            // WorldEdit / FAWE
            modrinth("fastasyncworldedit", "2.14.2")
            // WorldGuard
            modrinth("worldguard", "7.0.14")
            // Multiverse-Core
            modrinth("multiverse-core", "5.3.4")

            // PlaceholderAPI
            url("https://github.com/PlaceholderAPI/PlaceholderAPI/releases/download/2.11.7/PlaceholderAPI-2.11.7.jar")

            // DecentHolograms
            modrinth("decentholograms", "2.9.8")

            // TAB
            modrinth("tab-was-taken", "5.3.2")

            // LuckPerms
            modrinth("luckperms", "v5.5.17-bukkit")

            // VaultUnlocked
            modrinth("vaultunlocked", "2.17.0")

            // XConomy (Economy Provider)
            url("https://github.com/YiC200333/XConomy/releases/download/2.26.3/XConomy-Paper-2.26.3.jar")
        }
    }
    
    shadowJar {
        relocate 'com.github.stefvanschie.inventoryframework', 'org.nmo.project_exfil.inventoryframework'
    }
    
    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
